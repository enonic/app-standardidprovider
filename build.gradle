plugins {
    id 'java'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.2'
    id 'com.enonic.xp.app' version '3.2.0'
    id "com.github.node-gradle.node" version '5.0.0'
}

apply from: "$rootDir/gradle/defaults.gradle"

def xpVersion=version

app {
    name = 'com.enonic.xp.app.standardidprovider'
    group = 'com.enonic.xp'
    displayName = 'Standard ID Provider'
    systemVersion = "${xpVersion}"
    systemApp = true
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-node:${xpVersion}"
    include "com.enonic.lib:lib-mustache:2.1.1"
}

node {
    version = '18.12.1'
    download = true
}

task npmInstallForce( type: NpmTask ) {
    description = 'Update all project node dependencies'
    args = [ 'install', '--force' ]
}

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: '**/src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

task webpack( type: NpmTask, dependsOn: [npmInstall, lint] ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    description = 'Build UI resources (js, css, etc).'
    args = [ 'run', 'build' ]
    inputs.dir 'src/main'
    inputs.file 'webpack.config.js'
    outputs.dir "${buildDir}/resources/main"
}

task cleanNode( type: Delete ) {
    delete 'node_modules'
}

jar {
    def mainDir = file('build/resources/main')

    exclude 'assets/**/*.less'
    exclude 'assets/images/background.jpg'

    fileTree("$mainDir/assets/js") {
        exclude '_all.js'
    }.each { f -> exclude mainDir.toURI().relativize( file( f.path ).toURI() ).toString() }

    dependsOn += webpack
}

jar.outputs.dir "${buildDir}/resources/main"

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}
