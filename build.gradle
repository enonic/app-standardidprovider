plugins {
    id 'java'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.2'
    id 'com.enonic.xp.app' version '3.4.0'
    id "com.github.node-gradle.node" version '5.0.0'
}

apply from: "$rootDir/gradle/defaults.gradle"

def xpVersion=version

app {
    name = 'com.enonic.xp.app.standardidprovider'
    group = 'com.enonic.xp'
    displayName = 'Standard ID Provider'
    systemVersion = "${xpVersion}"
    systemApp = true
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-node:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "com.enonic.lib:lib-mustache:2.1.1"
    include "com.enonic.lib:lib-static:2.0.0-SNAPSHOT"
}

node {
    version = '18.12.1'
    download = true
}

task npmInstallForce( type: NpmTask ) {
    description = 'Update all project node dependencies'
    args = [ 'install', '--force' ]
}

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: '**/src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

task webpack( type: NpmTask, dependsOn: [npmInstall, lint] ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    description = 'Build UI resources (js, css, etc).'
    args = [ 'run', 'build' ]
    inputs.dir 'src/main'
    inputs.file 'webpack.config.js'
    outputs.dir "${buildDir}/resources/main"
}

task cleanNode( type: Delete ) {
    delete 'node_modules'
}

// Command to find if any new files types should be excluded
// find src -type f -a ! -name "*.java" ! -name "*.properties" ! -name "*.png" ! -name "*.jpg" ! -name "*.ttf" ! -name "*.woff" ! -name "*.eot" ! -name "*.svg" ! -name "*.json" ! -name "*.ico" ! -name "*.less" ! -name "*.js" ! -name "*.html" ! -name "*.xml"
processResources {
    // exclude '**/*.eot'
    // exclude '**/*.ico'
    // exclude '**/*.jpg'

    // this would hinder src/main/resources/idprovider/idprovider.js
    // exclude '**/*.js'

    // This seems to break everything
    // exclude 'assets/**/*.js'

    // this would hinder src/main/resources/assets/icons/favicons/manifest.json
    // exclude '**/*.json'

    exclude '**/*.less'

    // this would hinder src/main/resources/idprovider/idprovider.html
    // exclude '**/*.html'

    // exclude '**/*.png'
    // exclude '**/*.svg'
    // exclude '**/*.ttf'
    // exclude '**/*.woff'

    // this would hinder src/main/resources/idprovider/idprovider.xml
    // exclude '**/*.xml'
}

jar {
    def mainDir = file('build/resources/main')

    exclude 'assets/**/*.less'

    fileTree("$mainDir/assets/js") {
        exclude '_all.js'
    }.each { f -> exclude mainDir.toURI().relativize( file( f.path ).toURI() ).toString() }

    dependsOn += webpack
}

jar.outputs.dir "${buildDir}/resources/main"

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}
